# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=kirigami-addons
pkgname=(kirigami-addons5)
pkgver=0.11.0
pkgrel=3
pkgdesc='Add-ons for the Kirigami framework'
url='https://invent.kde.org/libraries/kirigami-addons'
arch=(x86_64)
license=(GPL LGPL)
depends=(gcc-libs
         glibc
         kirigami2
         qt5-base
         qt5-declarative
         qt5-quickcontrols2)
makedepends=(extra-cmake-modules
             ki18n5)
source=(https://download.kde.org/stable/$pkgbase/$pkgbase-$pkgver.tar.xz{,.sig})
sha256sums=('05296c5afbe6804308bf9c0d2751f3b748b40d00fa784946d1dcdf3af4bffbad'
            'SKIP')
validpgpkeys=(0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D  # Bhushan Shah (mykolab address) <bshah@mykolab.com>
              39FFA93CAE9C6AFC212AD00202325448204E452A) # Carl Schwan <carl@carlschwan.eu>

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package_kirigami-addons5() {

  DESTDIR="$pkgdir" cmake --install build
}
