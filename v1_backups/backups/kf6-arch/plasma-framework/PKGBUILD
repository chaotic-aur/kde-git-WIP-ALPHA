# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.240.0.20231014
pkgrel=1
pkgdesc='Plasma library and runtime components'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(gcc-libs
         glibc
         kactivities
         kcmutils
         kcolorscheme
         kconfig
         kcoreaddons
         kglobalaccel
         ki18n
         kiconthemes
         kio
         kirigami
         knotifications
         kpackage
         ksvg
         kwidgetsaddons
         kwindowsystem
         libglvnd
         libx11
         libxcb
         qt6-base
         qt6-declarative
         qt6-wayland
         wayland)
makedepends=(doxygen
             extra-cmake-modules
             kdoctools
             plasma-wayland-protocols
             qt6-doc
             qt6-tools)
groups=(kf6)
#source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
makedepends+=(git)
_commit=7f310799c9d4989e6259424888baa1ab7f2635b5
source=(git+https://invent.kde.org/frameworks/$pkgname#commit=$_commit)
sha256sums=('SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
