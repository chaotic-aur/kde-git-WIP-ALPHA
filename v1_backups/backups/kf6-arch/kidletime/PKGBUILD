# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kidletime
pkgver=5.240.0.20231014
pkgrel=1
pkgdesc='Monitoring user activity'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             libxss
             plasma-wayland-protocols
             qt6-doc
             qt6-tools
             qt6-wayland
             wayland-protocols)
optdepends=('libx11: XCB plugin'
            'libxcb: XCB plugin'
            'libxext: XCB plugin'
            'libxss: XCB plugin'
            'qt6-wayland: Wayland plugin'
            'wayland: Wayland plugin')
groups=(kf6)
#source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
makedepends+=(git)
_commit=415ae404f1d4eb9c6bc1ecba51ebdfc053920004
source=(git+https://invent.kde.org/frameworks/$pkgname#commit=$_commit)
sha256sums=('SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
